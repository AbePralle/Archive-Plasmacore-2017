#==============================================================================
# Plasmacore.rogue
#==============================================================================
$include "Anchor.rogue"
$include "Assets.rogue"
$include "Bitmap.rogue"
$include "Box.rogue"
$include "Color.rogue"
$include "Component.rogue"
$include "Corners.rogue"
$include "Display.rogue"
$include "Event.rogue"
$include "Font.rogue"
$include "Graphics.rogue"
$include "Image.rogue"
$include "Keyboard.rogue"
$include "LaunchOptions.rogue"
$include "Matrix.rogue"
$include "Messaging.rogue"
$include "OverlayLog.rogue"
$include "ProjectionMode.rogue"
$include "Quaternion.rogue"
$include "RenderMode.rogue"
$include "Rotation.rogue"
$include "Shader.rogue"
$include "Sprite.rogue"
$include "StarbrightGraphics.rogue"
$include "State.rogue"
$include "Texture.rogue"
$include "UpdateTimer.rogue"
$include "Window.rogue"
$include "XY.rogue"
$include "XYZ.rogue"
$include "XYZW.rogue"

module Plasmacore

class Plasmacore [requisite singleton]
  PROPERTIES
    next_resource_id = 1
    launch_handler : Function(LaunchOptions)
    display_state_types = Table<<String,TypeInfo>>()
    display_states = Table<<String,State>>()
    global_update_timer = UpdateTimer()

  METHODS
    method init
      Console.println "Initializing Plasmacore"

      MessageManager.add_handler( "Application.launch",
        function(m:Message)
          if (Plasmacore.launch_handler)
            local options = LaunchOptions()
            options.is_window_based = m.get_logical( "is_window_based" )
            Plasmacore.launch_handler( options )
          else
            Console.println "No Plasmacore.on_launch() handler is defined."
          endIf
        endFunction )

    method create_resource_id->Int32
      ++next_resource_id
      return (next_resource_id - 1)

    method find_asset( name:String )->String
      local filepath = native( "Plasmacore_find_asset( $name )" )->String
      if (filepath) return filepath

      name = "Assets/" + name
      return native( "Plasmacore_find_asset( $name )" )->String

    method get_state( display_name:String )->State
      local result = display_states[ display_name ]
      if (result) return result

      local type = display_state_types[ display_name ]
      if (not type) return null

      result = type.create_object<<State>>.init
      display_states[ display_name ] = result

      return result

    method global_update
      global_update_timer.update
      local state = get_state( "global" )
      if (not state) return

      while (global_update_timer.tick)
        state.handle_update
      endWhile

    method on_launch( handler:Function(LaunchOptions) )
      launch_handler = handler

    method set_state( display_name:String, state_type:TypeInfo )
      display_state_types[ display_name ] = state_type

    method set_state( display_name:String, state:State )
      display_states[ display_name ] = state
endClass

nativeCode extern "C" RogueString* Plasmacore_find_asset( RogueString* name );

