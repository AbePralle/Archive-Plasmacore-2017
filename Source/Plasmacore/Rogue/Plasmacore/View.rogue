module Plasmacore

class View : Canvas
  PROPERTIES
    window           : Window
    name             : String
    graphics         : Graphics
    logic            : ViewLogic
    pointer_position : XY
    pointer_exists   : Logical

  METHODS
    method init( window, name, logic )
      logic.view = this
      graphics = StarbrightGraphics( this )

    method draw
      Graphics = this.graphics
      Graphics.begin_draw( this )

      if (not logic.is_active) logic.is_active = true; logic.activate

      logic.draw
      Graphics.end_draw

    method handle( e:KeyEvent )
      logic.on( e )

    method handle( e:PointerEvent )
      if (pointer_exists)
        if (e.is_scroll) e.position = pointer_position
        else             e.delta = e.position - pointer_position
      else
        pointer_exists = true
        pointer_position = e.position
      endIf
      logic.handle( e )

    method update
      Graphics = this.graphics
      Graphics.begin_update( this )

      if (not logic.is_active) logic.is_active = true; logic.activate
      logic.update

      Graphics.end_update
endClass

