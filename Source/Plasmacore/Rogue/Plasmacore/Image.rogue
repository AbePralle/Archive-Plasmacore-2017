module Plasmacore

class Image
  PROPERTIES
    data   : ImageData

  METHODS
    method init( name:String )
      data = ImageManager.load( name )

    method init( bitmap:Bitmap )
      data = ImageManager.load( bitmap )

    method init( existing:Image, subset:Box )
      data = existing.data.create_subset( subset )

    method draw( position:XY, _size:XY, anchor:Anchor )
      if (not is_ready) return

      local transform = Graphics.transform
      position  -= (anchor.position * _size).floor
      local z    = -Graphics.unit_z

      local pt1  = transform * XYZ( position,          z )
      local pt2  = transform * XYZ( position+_size.xv, z )
      local pt3  = transform * XYZ( position+_size,    z )
      local pt4  = transform * XYZ( position+_size.yv, z )

      data.draw( pt1, pt2, pt3, pt4 )

    method draw( position:XY, _size:XY, anchor:Anchor, rotation:Rotation )
      draw( position, _size, anchor, Quaternion(rotation) )

    method draw( position:XY, _size:XY, anchor:Anchor, rotation:Quaternion )
      if (not is_ready) return

      local handle = XYZ( (anchor.position * _size).floor, 0 )
      local z      = -Graphics.unit_z
      local origin = XYZ( position, z ) + handle

      Graphics.push_object_transform( rotation.to_matrix(origin,handle) )
      #Graphics.push_object_transform( Matrix.identity )

      local transform = Graphics.transform

      local pt1  = transform * XYZ( position,          z )
      local pt2  = transform * XYZ( position+_size.xv, z )
      local pt3  = transform * XYZ( position+_size,    z )
      local pt4  = transform * XYZ( position+_size.yv, z )

      data.draw( pt1, pt2, pt3, pt4 )

      Graphics.pop_object_transform


    method draw( pt1:XYZW, pt2:XYZW, pt3:XYZW, pt4:XYZW )
      if (prepare) data.draw( pt1, pt2, pt3, pt4 )

    method height->Real64
      return data.bounds.size.y

    method is_ready->Logical
      return prepare

    method prepare->Logical
      if (not data.is_ready) data = data.prepare
      return data.is_ready

    method size->XY
      return data.bounds.size

    method width->Real64
      return data.bounds.size.x
endClass

class ImageData
  PROPERTIES
    bounds      : Box
    shader      : Shader
    render_mode : RenderMode
    version     : Int32
    # Used to keep this images in sync with textures

  METHODS
    method draw( pt1:XYZW, pt2:XYZW, pt3:XYZW, pt4:XYZW )

    method create_subset( subset:Box )->ImageData
      return ImageData()

    method is_ready->Logical
      return false

    method prepare->ImageData
      return this
endClass

class UnresolvedImageData( name:String ) : ImageData
  METHODS
    method prepare->ImageData
      if (Graphics.is_ready and ImageManager.is_ready)
        return ImageManager.load( name )
      else
        return this
      endIf
endClass

class TextureImageData : ImageData
  PROPERTIES
    texture     : Texture
    is_subset   : Logical
    uv1         : XY
    uv2         : XY

  METHODS
    method init( texture )
      shader      = Shader.TEXTURE
      render_mode = RenderMode.IMAGE_DEFAULT
      uv1         = XY(0,0)
      uv2         = texture.image_size / texture.texture_size
      bounds      = Box( 0, 0, texture.image_size )
      version     = texture.version

    method init( texture, bounds )
      shader      = Shader.TEXTURE
      render_mode = RenderMode.IMAGE_DEFAULT
      uv1         = bounds.top_left / texture.texture_size
      uv2         = bounds.bottom_right / texture.texture_size
      is_subset   = true
      version     = texture.version

    method create_subset( subset:Box )->ImageData
      return TextureImageData( texture, subset )

    method draw( pt1:XYZW, pt2:XYZW, pt3:XYZW, pt4:XYZW )
      Graphics.draw_textured_quad( texture, shader, render_mode,
          pt1, pt2, pt3, pt4, uv1, XY(uv2.x,uv1.y), uv2, XY(uv1.x,uv2.y) )

    method is_ready->Logical
      return texture.is_ready and version == texture.version

    method prepare->ImageData
      # The graphics context changed or a backing bitmap was updated;
      # find or load the texture in the current context
      texture = texture.reload
      version = texture.version
      if (not is_subset)
        # Adjust the bounds to match the new texture size
        bounds = Box( 0, 0, texture.image_size )
      endIf

      # Recalculate the UV
      uv1  = bounds.top_left / texture.texture_size
      uv2  = bounds.bottom_right / texture.texture_size

      return this

endClass


class ImageManager [singleton]
  METHODS
    method is_ready->Logical
      return true

    method load( name:String )->ImageData
      if (not Graphics.is_ready) return UnresolvedImageData( name )
      local texture = Graphics.acquire_texture( name )
      return TextureImageData( texture )

    method load( bitmap:Bitmap )->ImageData
      local texture = Graphics.acquire_texture( bitmap )
      return TextureImageData( texture )
endClass

