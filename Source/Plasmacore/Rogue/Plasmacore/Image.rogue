module Plasmacore

class Image
  PROPERTIES
    data   : ImageData

  METHODS
    method init( name:String )
      data = ImageManager.load( name )

    method init( bitmap:Bitmap )
      data = ImageManager.load( bitmap )

    method init( existing:Image, subset:Box )
      data = existing.data.create_subset( subset )

    method draw( pos:XYZ )
      if (is_ready) data.draw( pos )

    method height->Real64
      return data.size.y

    method is_ready->Logical
      if (not data.is_ready) data = data.prepare
      return data.is_ready

    method width->Real64
      return data.size.x
endClass

class ImageData
  PROPERTIES
    size : XY

  METHODS
    method draw( pos:XYZ )

    method create_subset( subset:Box )->ImageData
      return ImageData()

    method is_ready->Logical
      return false

    method prepare->ImageData
      return this
endClass

class UnresolvedImageData( name:String ) : ImageData
  METHODS
    method prepare->ImageData
      if (Graphics.is_ready and ImageManager.is_ready)
        return ImageManager.load( name )
      else
        return this
      endIf
endClass

class TextureImageData : ImageData
  PROPERTIES
    texture : Texture
    uv1     : XY
    uv2     : XY

  METHODS
    method init( texture )

    method create_subset( subset:Box )->ImageData
      local result = TextureImageData( texture )
      result.size = subset.size
      result.uv1 = subset.top_left / texture.texture_size
      result.uv2 = subset.bottom_right / texture.texture_size
      return result

    method draw( pos:XYZ )
      local hw = size.x / 2
      local hh = size.y / 2
      local pt1 = XYZ( -hw, -hh, 0 )
      local pt2 = XYZ(  hw, -hh, 0 )
      local pt3 = XYZ(  hw,  hh, 0 )
      local pt4 = XYZ( -hw,  hh, 0 )

      local uv21 = XY(uv2.x,uv1.y)
      local uv12 = XY(uv1.x,uv2.y)

      Graphics.push_object_transform( Matrix.translate(XYZ(pos.x,pos.y,-pos.z)) )

      Graphics.draw_textured_quad( texture, Shader.TEXTURE, RenderMode.IMAGE_DEFAULT, #.enable_point_filter,
          pt1, pt2, pt3, pt4, uv1, uv21, uv2, uv12 )

      Graphics.pop_object_transform

    method is_ready->Logical
      return texture.is_ready

    method prepare->ImageData
      # The graphics context changed or a backing bitmap was updated;
      # find or load the texture in the current context
      texture = texture.reload
      size = texture.image_size

      return this

endClass


class ImageManager [singleton]
  METHODS
    method is_ready->Logical
      return true

    method load( name:String )->ImageData
      if (not Graphics.is_ready) return UnresolvedImageData( name )
      local texture = Graphics.acquire_texture( name )
      local image_data = TextureImageData( texture )
      image_data.size = texture.image_size
      image_data.uv1  = XY(0,0)
      image_data.uv2  = texture.uv_size
      return image_data

    method load( bitmap:Bitmap )->ImageData
      local texture = Graphics.acquire_texture( bitmap )
      local image_data = TextureImageData( texture )
      image_data.size = texture.image_size
      image_data.uv1  = XY(0,0)
      image_data.uv2  = texture.uv_size
      return image_data
endClass

