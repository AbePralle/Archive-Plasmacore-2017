module Plasmacore


class KeyEvent
  PROPERTIES
    keycode     : Int32
    unicode     : Character
    syscode     : Int32
    is_press    : Logical
    is_repeat   : Logical
    is_consumed : Logical

  METHODS
    method init( keycode, unicode, syscode, is_press=false, is_repeat=false )
      is_consumed = false  # Need to be explicit because KeyEvent is recycled.

    method character->Character [macro]
      this.unicode->Character

    method consume [macro]
      this.is_consumed = true

    method is_character( ch:Character )->Logical [macro]
      (this.unicode == ch)

    method is_press( ch:Character )->Logical
      return unicode == ch and is_press

    method is_repeat( ch:Character )->Logical
      return unicode == ch and is_repeat

    method is_release->Logical [macro]
      not this.is_press

    method is_release( ch:Character )->Logical
      return unicode == ch and not is_press

    method to->String
      local buffer = StringBuilder()
       which (unicode)
         case    8: buffer.print( "[Backspace] " )
         case '\t': buffer.print( "[Tab] " )
         case '\n': buffer.print( "[Enter] " )
         case   32: buffer.print( "[Space] " )
         case  127: buffer.print( "[Delete] " )
         others
           buffer.print( "'" ).print( unicode ).print( "' " )
           if (unicode > 128) buffer.print( "(Unicode " ).print( Int32(unicode) ).print( ") " )
       endWhich

       if (is_press)
         if (is_repeat) buffer.print( "pressed (repeating)" )
         else           buffer.print( "pressed" )
       else
         buffer.print( "released" )
       endIf

        if (is_consumed) buffer.print( " (consumed)" )

       return buffer->String

endClass


class PointerEvent
  ENUMERATE
    MOVEMENT = 0
    PRESS    = 1
    RELEASE  = 2
    SCROLL   = 3

  PROPERTIES
    type        : Int32
    position    : XY
    delta       : XY
    button      : Int32
    id          : Int32
    is_consumed : Logical

  METHODS
    method init( type, position, delta, button, id )
      is_consumed = false  # Need to be explicit because PointerEvent is recycled.

    method consume [macro]
      this.is_consumed = true

    method is_movement->Logical [macro]
      (this.type == PointerEvent.MOVEMENT)

    method is_press->Logical [macro]
      (this.type == PointerEvent.PRESS)

    method is_press( button_index:Int32 )->Logical
      return (this.type == PRESS and button == button_index)

    method is_release->Logical [macro]
      (this.type == PointerEvent.RELEASE)

    method is_release( button_index:Int32 )->Logical
      return (this.type == RELEASE and button == button_index)

    method is_scroll->Logical [macro]
      (this.type == PointerEvent.SCROLL)

    method to->String
      local buffer = StringBuilder()
      buffer.print( "Pointer " ).print( id ).print( " " )
      which (type)
        case MOVEMENT: buffer.print( "movement" )
        case PRESS:    buffer.print( "press button " ).print( button )
        case RELEASE:  buffer.print( "release" )
        case SCROLL:   buffer.print( "scroll " ).print( delta )
      endWhich
      buffer.print( " at " ).print( position )
      if (is_consumed) buffer.print( " (consumed)" )

      return buffer->String
endClass

