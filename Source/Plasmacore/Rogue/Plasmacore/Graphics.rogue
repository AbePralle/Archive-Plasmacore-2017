module Plasmacore

class Graphics [singleton]
  GLOBAL PROPERTIES
    stack = Graphics[]

  PROPERTIES
    canvas            : Canvas
    textures          = Table<<String,Texture>>()

    size              : XY
    true_size         : XY
    background_color  : Int32

    transform         : Matrix
    view_transform    : Matrix
    object_transform  : Matrix
    view_transforms   = Matrix[]
    object_transforms = Matrix[]
    view_transform_modified   : Logical
    object_transform_modified : Logical

    unit_z            : Real64


  METHODS
    method init( canvas )
      size = canvas.size
      true_size = size
      background_color = Color.BLACK

    method acquire_texture( name:String )->Texture
      local entry = textures.find( name )
      if (entry) return entry.value

      local texture = Texture( name )
      textures[ name ] = texture
      TextureManager.load( texture )
      return texture

    method begin_draw( canvas )->this
      size = canvas.size
      set_mode_2dx
      return this

    method bounds->Box
      return Box( XY(0,0), size )

    method clear
      # Override

    method clear( background_color )
      clear

      #{
    method define_texture( name:String, bitmap:Bitmap )->Texture
      return Texture( name, 0, bitmap.size, XY(1,1) )
      }#

    method draw_textured_quad( texture:Texture, shader:Shader, render_mode:RenderMode,
        pt1:XYZ, pt2:XYZ, pt3:XYZ, pt4:XYZ, uv1:XY, uv2:XY, uv3:XY, uv4:XY )

    method draw_textured_triangle( texture:Texture, shader:Shader, render_mode:RenderMode,
        pt1:XY, pt2:XY, pt3:XY, uv1:XY, uv2:XY, uv3:XY )

    method end_draw->this
      return this

    method height->Int32 [macro]
      this.size.y->Int32

    method flush

    method load_texture( texture:Texture, file:File )
      println ''Failed to load texture "$" from file "$".'' (texture.name,file)

    method push( g:Graphics )->Graphics
      stack.add( this )
      Graphics = g
      return g

    method push_object_transform( m:Matrix )->this
      object_transforms.add( m )
      object_transform_modified = true
      return this

    method push_view_transform( m:Matrix )->this
      view_transforms.add( m )
      view_transform_modified = true
      return this

    method pop->Graphics
      if (stack.count == 0) Graphics = Graphics()
      else                  Graphics = stack.remove_last
      return Graphics

    method pop_object_transform->this
      if (object_transforms.count)
        object_transforms.remove_last
        object_transform_modified = true
      endIf
      return this

    method pop_view_transform->this
      if (view_transforms.count)
        view_transforms.remove_last
        view_transform_modified = true
      endIf
      return this

    method set_mode_2dx( _size=Graphics.size:XY, near_scale=384:Real64, max_z=16:Real64 )

    method set_transform

    method width->Int32 [macro]
      this.size.x->Int32
endClass

