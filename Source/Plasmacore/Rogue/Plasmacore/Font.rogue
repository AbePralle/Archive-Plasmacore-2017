module Plasmacore

class Font
  GLOBAL PROPERTIES
    work_buffer = StringBuilder()

  PROPERTIES
    name   : String
    height : Int32
    data   : FontData
    anchor = Anchor.TOP_LEFT : Anchor

  METHODS
    method draw( text:String, position:XY )
      local cursor = position
      forEach (ch in text)
        local info = this[ ch ]
        if (info)
          local img = info.image
          img.draw( cursor, info.size, Anchor.TOP_LEFT )
          cursor.x += info.size.x
        endIf
      endForEach

    method text_size( text:String )->XY
      local result = XY(0,0)
      forEach (ch in text)
        local info = this[ ch ]
        if (info) result += info.size
      endForEach
      return result

    method text_size( text:StringBuilder )->XY
      local result = XY(0,0)
      forEach (ch in text)
        local info = this[ ch ]
        if (info) result += info.size
      endForEach
      return result

    method text_size( ch:Character )->XY
      local info = this[ ch ]
      if (not info) return XY(0,0)
      return info.size

    method get( ch:Character )->FontCharacter
      return data[ ch ]
endClass

class BitmapFont : Font
  PROPERTIES
    width  : Int32

  METHODS
    method init( name, width, height, characters=' '..'~':Range<<Character>> )
      data = FontManager.font_data[ name ]

      if (data) return

      local filepath = Plasmacore.find_asset( name )
      if (not filepath)
        println "ERROR loading BitmapFont - no such file $." (name)
        return
      endIf

      local src = Bitmap( File(filepath) )
      if (src.error) return

      data = FontData( height )
      FontManager.font_data[ name ] = data

      local src_w = src.width / width
      local src_h = src.height / height

      forEach (j in 0..<src_h)
        forEach (i in 0..<src_w)
          local x = i * width
          local y = j * height
          local ch_data = Bitmap( src, Box(x,y,width,height) )
          data.define( characters.read, ch_data )
        endForEach
      endForEach

      prepare

    method prepare
      if (data) data.prepare

endClass


class FontData
  PROPERTIES
    bitmap      : Bitmap
    image       : Image
    height      : Int32
    cursor_x=1  : Int32  # For writing new characters into the font bitmap
    cursor_y=1  : Int32
    ascii       = FontCharacter[]( 128, null )
    lookup      : Table<<Character,FontCharacter>>  # Note: does not contain the ASCII characters
    is_modified : Logical

  METHODS
    method init( height )
      bitmap = Bitmap( 1024, height+2 )
      image = Image( bitmap )

    method define( ch:Character, data:Bitmap )
      is_modified = true
      local w = data.width

      if (cursor_x + w+1 > bitmap.width)
        cursor_x = 1
        cursor_y += height+2
        bitmap.add_rows( height+2 )
      endIf

      if (data.height > height)
        # This character is taller than the nominal max height of the font.
        # Make room for it and adjust the height.
        bitmap.add_rows( data.height - height )
        height = data.height
      endIf

      data.blit( bitmap, XY(cursor_x,cursor_y) )
      bitmap.extend_edges( Box(cursor_x,cursor_y,w,height) )

      local info = FontCharacter( this, ch, XY(cursor_x,cursor_y), XY(w,height) )
      if (ch <= 127)
        ascii[ ch ] = info
      else
        if (not lookup) lookup = Table<<Character,FontCharacter>>()
        lookup[ ch ] = info
      endIf

      cursor_x += w + 2

    method get( ch:Character )->FontCharacter
      if (ch <= 127) return ascii[ ch ]
      if (lookup)    return lookup[ ch ]
      return null

    method prepare
      if (not is_modified) return
      is_modified = false

      if (image)
        # Changing the bitmap version will automatically update the texture
        # when the image is prepare()d.  Other images based on the texture
        # (such as character images) will automatically update as well.
        ++bitmap.version
        image.prepare
      else
        image = Image( bitmap )
      endIf

endClass


class FontCharacter
  GLOBAL PROPERTIES
    shared_image : Image

  PROPERTIES
    font_data : FontData
    unicode   : Character
    top_left  : XY
    size      : XY
    offset_x  : Int32
    offset_y  : Int32

  METHODS
    method init( font_data, unicode, top_left, size )

    method image->Image
      if (shared_image)
        shared_image.adjust_subset( Box(top_left,size) )
      else
        shared_image = Image( font_data.image, Box(top_left,size) )
      endIf
      return shared_image

endClass


class FontManager [singleton]
  PROPERTIES
    font_data = Table<<String,FontData>>()
endClass
