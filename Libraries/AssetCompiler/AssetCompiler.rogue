$include "../Rogue/Plasmacore"
using Plasmacore

$include "ImageCompiler.rogue"
$include "DataCompiler.rogue"
$include "SoundCompiler.rogue"

launch_image_compiler
launch_sound_compiler
launch_data_compiler

class CommentStripper
  PROPERTIES
    reader        : StringReader
    lines         = String[]
    buffer        = StringBuilder()
    comment_level : Int32

  METHODS
    method process( text:String )->String[]
      reader = text.reader
      while (reader.has_another) scan_next
      if (buffer.count) lines.add( buffer->String )
      return lines

    method scan_next
      local ch = reader.read
      which (ch)
        case '#'
          if (reader.peek == '{')
            # Multiline comment
            reader.read  # '{'
            ++comment_level
          else
            # Single line comment
            while (reader.has_another and reader.peek != '\n') reader.read
          endIf

        case '}'
          if (comment_level and reader.peek == '#')
            reader.read
            --comment_level
          else
            write( '}' )
          endIf

        case '"', '\''
          # "string"
          local quote_type = ch
          write( ch )
          while (reader.has_another)
            ch = reader.read
            write( ch )
            which (ch)
              case '\\':       write( reader.read ) # skip past next
              case quote_type: escapeWhile
            endWhich
          endWhile

        others
          write( ch )

      endWhich

    method write( ch:Character )->this
      if (ch == '\n')
        lines.add( buffer->String )
        buffer.clear
      elseIf (comment_level == 0)
        buffer.print( ch )
      endIf

      return this

endClass

