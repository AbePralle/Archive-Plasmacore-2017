$include "../Rogue/Plasmacore"
using Plasmacore

try
  local config_file = File( "Assets/Images/ImageConfig.txt" )
  if (not config_file.exists)
    File.create_folder( config_file.path )
    local writer = PrintWriter(config_file.writer)
    writer.println @|# Default image config file - customize as desired.
                    |#
                    |# PLATFORMS
                    |#   [Global]  - Use this category for attributes and settings
                    |#               that apply to all platforms.
                    |#   [Default] - Used when specific platform isn't defined, e.g.
                    |#               if [iOS] isn't defined then [Default] is used.
                    |#   [iOS]     - iOS-specific attributes, settings, and groups.
                    |#
                    |#
                    |# COMMANDS
                    |#   @ <file or pattern> [attributes...]  Set image attributes
                    |#   set image_sheet_size:4096            Set max texture size
                    |#   set display_scale:1/2                Images drawn half size etc.
                    |#   group <folder or pattern>            Turn folders into groups
                    |#   copy from:<folder> to:<folder> [rescale:1/2] [resize:WxH->WxH]
                    |#
                    |# ATTTRIBUTES
                    |#   @ ... resample <pixel width>x<pixel height>
                    |#     Change pixel size but leave display size as it was.
                    |#   @ ... split:<tiles-wide>x<tiles-high>
                    |#   @ ... varfont
                    |#   @ ... varfont:"Character Mapping"
                    |#   @ ... varfont [characters:"ABC..." unicode:[32-126] ...]
                    |#
                    |[Global]
                    |@ **Font* varfont
                    |@ **font* varfont
                    |
                    |[Default]
                    |set image_sheet_size:4096
                    |group .
    writer.close
  endIf



  local args = System.command_line_arguments
  if (args.count)
    ImageCompiler( config_file, (forEach in args) )
  else
    ImageCompiler( config_file, "Default" )
  endIf

catch (err:Error)
  println err
  System.exit 1
endTry

class Bitmaps : Table<<String,Bitmap>> [singleton]
  METHODS
    method get( name:String )->Bitmap
      local bitmap = prior.get( name )
      if (bitmap) return bitmap

      bitmap = Bitmap( File(name) )
      this[ name ] = bitmap

      return bitmap
endClass

class BuildError( cmd:Command, message ) : Error
  METHODS
    method init( message )

    method to->String
      local builder = StringBuilder()

      local w = Console.width.or_smaller( 79 )
      local bar = "=" * w
      builder.println bar
      if (cmd)
        builder.println( (''IMAGE COMPILE ERROR in "$" line $'' (cmd.src_filepath,cmd.src_line)).word_wrapped(w) )
      else
        builder.println "IMAGE COMPILE ERROR"
      endIf
      builder.println
      builder.println message.word_wrapped( w )
      builder.println bar
      return builder->String
endClass

class ImageCompiler
  PROPERTIES
    file             : File
    file_timestamp   : Real64
    platform         : String
    commands         = Table<<String,Command[]>>()
    attributes       = @[]
    settings         = @{}
    image_sheet_size = 2048
    group_manifests  = @[]
    newest_group_manifest_timestamp : Real64
    even_odd         = true
    files_written    : Int32
    all_images       : String[]

  METHODS
    method init( file, platform )
      println "Compiling images for platform $..." (platform)
      file_timestamp = file.timestamp
      group_manifests = @[]
      newest_group_manifest_timestamp = 0.0

      organize_commands
      execute_global_commands
      execute_platform_commands

      local manifest_filepath = "Build/$/Assets/Images/ImageManifest.json" (platform)
      File.create_folder( File.folder(manifest_filepath) )
      if (not File.exists(manifest_filepath) or File.timestamp(manifest_filepath) < newest_group_manifest_timestamp)
        ++files_written
        println "  Writing " + manifest_filepath
        @{ groups:group_manifests }.save( File(manifest_filepath) )
      endIf

      if (not files_written)
        println "Images up to date."
      endIf

    method execute_global_commands
      if (not commands.contains("Global")) return
      (forEach in commands//Global).execute

    method execute_platform_commands
      local working_platform = select{ commands.contains(platform):platform || "Default" }
      if (commands.contains(working_platform))
        (forEach in commands[working_platform]).execute
      else
        if (platform != "Default")
          println "IMAGE COMPILER WARNING: No configuration for platforms $ or Default in Assets/Images/ImageConfig.txt." (platform)
        else
          println "IMAGE COMPILER WARNING: No configuration for platform Default in Assets/Images/ImageConfig.txt."
        endIf
      endIf

    method organize_commands
      local category = "Global"

      forEach (line at index in CommentStripper().process(file.load_as_string))
        line = line.trimmed
        if (line.count == 0) nextIteration
        if (line.begins_with('['))
          local after = line.after_first(']')
          if (after.count) throw BuildError( Command(this,file.filepath,index+1,line), "Syntax error after ']'." )
          category = line.after_first( '[' ).before_first( ']' ).trimmed
        else
          ensure commands[ category ]
          commands[ category ].add( Command(this,file.filepath,index+1,line) )
        endIf
      endForEach

endClass


class Command
  PROPERTIES
    icom         : ImageCompiler
    src_filepath : String
    src_line     : Int32
    content      : String
    reader       : ParseReader
    buffer       = StringBuilder()

  METHODS
    method init( icom, src_filepath, src_line, content )
      reader = ParseReader( content )

    method collect_files( filepath:String )->String[]
      local pattern = filepath
      if (File.exists(filepath))
        pattern = "**"
      else
        while (filepath.contains('*') or filepath.contains('?')) filepath = File.folder( filepath )
        if (not File.exists(filepath)) throw error( ''No such file or folder "$".'' (filepath) )
      endIf

      local file_list = String[]

      if (File.is_folder(filepath))
        forEach (f in File.listing(filepath,&ignore_hidden,&recursive))
          if (File.matches_wildcard_pattern(f,pattern)) file_list.add( f )
        endForEach
      else
        file_list.add( filepath )
      endIf

      local filtered_list = String[]
      forEach (f in file_list)
        if (not File.is_folder(f)) filtered_list.add( f )
      endForEach

      return filtered_list

    method consume( ch:Character )->Logical
      return (reader.consume(ch))

    method consume( text:String )->Logical
      return (reader.consume(text))

    method consume_whitespace->Logical
      local found_any = false

      while (reader.consume_spaces or reader.consume('\t'))
        found_any = true
      endWhile

      return found_any

    method error( message:String )->BuildError
      return BuildError( this, message )

    method execute
      if (consume("@"))
        local pattern = parse_string( "Filename or file pattern expected after \"@\"." )
        pattern = "Assets/Images/" + pattern
        local info = @{ pattern:pattern, attributes:[] }
        local characters : StringBuilder
        while (has_another)
          local name = parse_string( "Attribute or [end of line] expected." )
          consume_whitespace
          if (consume(':'))
            local value = parse_string( "Value expected." )
            if (name == "varfont")
              ensure characters
              characters.print( value )
              local attribute = @{ name:name }
              info//attributes.add( attribute )
            elseIf (name == "characters")
              ensure characters
              characters.print( value )
            elseIf (name == "unicode")
              ensure characters
              if (value.begins_with('['))
                local ranges = value.after_first('[').before_last(']').replacing(',',' ').split(' ')
                forEach (range in ranges)
                  if (range.contains('-'))
                    local parts = range.split( '-' )
                    characters.print( parts.first->Int32->Character )
                    characters.print( parts.last->Int32->Character )
                  else
                    characters.print( range->Int32->Character )
                  endIf
                endForEach
              else
                characters.print( value->Int32->Character )
              endIf
            else
              local attribute = @{ name:name }
              attribute//value = value
              info//attributes.add( attribute )
            endIf
          else
            local attribute = @{ name:name }
            info//attributes.add( attribute )
          endIf
        endWhile
        if (characters)
          forEach (attr in info//attributes)
            if (attr//name == "varfont") attr//value = characters->String
          endForEach
        endIf
        icom.attributes.add( info )
        return
      endIf

      local cmd = parse_string( "Command expected." )
      which (cmd)
        case "group"
          if (not icom.all_images) icom.all_images = File.listing( "Assets/Images", &recursive, &ignore_hidden )
          compile_group

        case "copy"
          icom.all_images = null
          local args = parse_args
          require_arg( args, "from" )
          require_arg( args, "to" )
          image_copy( args )

        case "set"
          local setting = parse_name_value
          icom.settings[ setting//name ] = setting//value

        others
          throw error( ''Syntax error - unknown command "$".'' (cmd) )
      endWhich

    method compile_group
      local group_name = parse_string( "Folder or file pattern expected." )
      if (group_name == ".")
        compile_group( "Assets/Images" )
      else
        group_name = "Assets/Images/" + group_name
        if (group_name.contains("*") or group_name.contains('?'))
          forEach (filepath in icom.all_images)
            if (File.matches_wildcard_pattern(filepath,group_name))
              compile_group( filepath )
            endIf
          endForEach
        else
          compile_group( "Assets/Images/" + group_name )
        endIf
      endIf

    method compile_group( group_name:String )
      local pattern = group_name
      group_name = File.before_wildcard_pattern( pattern.after_first("Assets/") )
      local output_filename = group_name.replacing('/','-')

      local dest_folder = "Build/$/Assets/Images" (icom.platform)
      File.create_folder( dest_folder )

      local oldest_existing_timestamp = 0.0
      forEach (filepath in File.listing("$/$*.png" (dest_folder,output_filename)))
        local timestamp = File.timestamp( filepath )
        if (oldest_existing_timestamp == 0.0 or oldest_existing_timestamp > timestamp)
          oldest_existing_timestamp = timestamp
        endIf
      endForEach

      local manifest_folder = "Build/$/Intermediate/ImageGroupManifests" (icom.platform)
      File.create_folder( manifest_folder )
      local manifest_filepath = "$/$.json" (manifest_folder,output_filename)
      local manifest_timestamp = 0.0
      if (File.exists(manifest_filepath)) manifest_timestamp = File.timestamp( manifest_filepath )
      oldest_existing_timestamp = oldest_existing_timestamp.or_smaller( manifest_timestamp )

      #local args = parse_args
      local files = collect_files( pattern )

      contingent
        #necessary (icom.file_timestamp < oldest_existing_timestamp)
        forEach (filepath in files)
          necessary (File.timestamp(filepath) < oldest_existing_timestamp)
        endForEach

        # No changes to group.  Load up cached group manifest so it can be written out
        # with the platform manifest
        icom.group_manifests.add( JSON.load_table( manifest_filepath ) )
        icom.newest_group_manifest_timestamp = icom.newest_group_manifest_timestamp.or_larger( manifest_timestamp )

        return  # no changes to group
      endContingent

      println "Group " + group_name

      local assets = Asset[]
      forEach (filepath in files)
        local ext = File.extension(filepath).to_lowercase

        if (ext == "png" or ext == "jpeg" or ext == "jpg")
          assets.add( create_asset_object(filepath) )
        endIf
      endForEach

      if (not assets.count)
        println "Skipping empty group " + group_name
        return
      endIf

      local image_sheets = perform_layout( assets )

      # Build group manifest while rendering image sheets
      local manifest = @{ name:group_name }

      local textures = manifest.ensure_list( "textures" )
      forEach (sheet at index in image_sheets)
        sheet.filename = "$-$.png" (output_filename,index.right_justified(2,'0'))
        local sheet_filepath = "Build/$/Assets/Images/$" (icom.platform,sheet.filename)
        ++icom.files_written
        print "  Writing "
        print sheet_filepath
        print " "
        if (icom.even_odd)
          forEach (1..(50 - sheet.filename.count)) print "."
        else
          forEach (1..(50 - sheet.filename.count)) print " "
        endIf
        icom.even_odd = not icom.even_odd
        println " [$x$]" (sheet.width,sheet.height)

        textures.add(
        @{
          filename: sheet.filename,
          size: { width:sheet.width, height:sheet.height }
        } )

        File.save( sheet_filepath, sheet.render.to_png_bytes )

      endForEach

      local images = manifest.ensure_list( "images" )
      forEach (asset in assets)
        asset.collect_image_info( images )
      endForEach

      # Store group manifest
      icom.group_manifests.add( manifest )
      manifest_timestamp = System.time
      icom.newest_group_manifest_timestamp = icom.newest_group_manifest_timestamp.or_larger( manifest_timestamp )

      manifest.save( File(manifest_filepath), &formatted )


    method perform_layout( assets:Asset[] )->ImageSheet[]
      local placements = Placement[]
      (forEach in assets).collect_placements( placements )

      local max_w = select{ icom.settings//image_sheet_size->Int32 || 2048 }.to_power_of_two
      local max_h = max_w

      local sheets = ImageSheet[]
      while (placements.count)
        local sheet = perform_layout( placements, max_w, max_h )
        if (not sheet)
          local w = placements.first.width
          local h = placements.first.height
          local min_size = w.to_power_of_two.or_larger( h.to_power_of_two )
          throw error( "$ ($x$) is too large to fit on a $x$ image sheet.  Use the 'set image_sheet_size WxH' directive " ...
                  "to increase the group's maximum image sheet size to $x$ or greater." ...
                  (placements.first.asset.filepath,w,h,max_w,max_h,min_size,min_size) )
          return null
        endIf
        sheets.add( sheet )
      endWhile

      require sheets.count

      # Try reducing the size of the last sheet by redoing the layout
      local last_sheet = sheets.remove_last

      loop
        if (last_sheet.height >= last_sheet.width)
          placements.clear.add( last_sheet.placements )
          local better_fit = perform_layout( placements, last_sheet.width, last_sheet.height/2 )
          if (better_fit and placements.count == 0)
            last_sheet = better_fit
            nextIteration
          endIf

        else
          placements.clear.add( last_sheet.placements )
          local better_fit = perform_layout( placements, last_sheet.width/2, last_sheet.height )
          if (better_fit and placements.count == 0)
            last_sheet = better_fit
            nextIteration
          endIf

        endIf

        escapeLoop
      endLoop

      # Redo the last sheet layout one final time with the best dimensions - any failed
      # attempts above have left the images positioned badly.
      placements.clear.add( last_sheet.placements )
      last_sheet = perform_layout( placements, last_sheet.width, last_sheet.height )

      sheets.add( last_sheet )

      return sheets

    method perform_layout( placements:Placement[], max_width:Int32, max_height:Int32 )->ImageSheet
      InsertionSort<<Placement>>.sort( placements,
        function(a:Placement,b:Placement)->(Logical)
          # 'a' should come before 'b' when:
          if (a.height > b.height) return true
          if (a.height < b.height) return false
          # Heights are equal

          return (a.width > b.width)
        endFunction
      )

      local sheet = ImageSheet( max_width, max_height )
      if (perform_layout(placements, -1, -1, max_width+2, max_height+2, sheet)) return sheet
      else return null

    method perform_layout( placements:Placement[], sx:Int32, sy:Int32, sw:Int32, sh:Int32, image_sheet:ImageSheet )->Logical
      # Find the largest asset we can place (including a 1-pixel border around
      # the edge), delete it from the list, and then recursively place any
      # remaining placements we can.
      forEach (placement in placements)
        local w = (placement.width  + 2)->Int32
        local h = (placement.height + 2)->Int32
        if (w <= sw and h <= sh)
          placement.x = sx+1
          placement.y = sy+1
          placements.remove( placement )
          image_sheet.add_placement( placement )

          # We've just placed 'A', now recursively fill areas 'B' and 'C':
          #
          # AAA|BBBBBB
          # AAA|BBBBBB
          # ---+------
          # CCCCCCCCCC
          # CCCCCCCCCC
          # CCCCCCCCCC
          perform_layout( placements, sx+w, sy,   sw-w, h,    image_sheet )   # B
          perform_layout( placements, sx,   sy+h, sw,   sh-h, image_sheet )   # C

          return true
        endIf
      endForEach

      # Unable to place any
      return false

    method create_asset_object( filepath:String )->Asset
      local attributes = @{}
      forEach (attr in icom.attributes)
        if (File.matches_wildcard_pattern(filepath,attr//pattern))
          forEach (a in attr//attributes)
            if (a//value) attributes[ a//name ]         = a//value
            else          attributes[ a//name->String ] = true
          endForEach
        endIf
      endForEach

      return Asset( this, filepath, attributes )

    method has_another->Logical
      consume_whitespace
      return (reader.has_another)

    method image_copy( args:@{} )
      require_arg( args, "from" )
      require_arg( args, "to" )
      local from_filepath = args//from->String
      local to_filepath   = args//to->String
      if (from_filepath.begins_with("Images/")) from_filepath = "Assets/" + from_filepath
      if (to_filepath.begins_with("Images/"))   to_filepath = "Assets/" + to_filepath
      if (not from_filepath.begins_with("Assets/Images/")) from_filepath = "Assets/Images/" + from_filepath
      if (not to_filepath.begins_with("Assets/Images/"))   to_filepath = "Assets/Images/" + to_filepath
      local files = collect_files( from_filepath )
      forEach (src_filepath in files)
        local from_folder = File.before_wildcard_pattern( from_filepath )
        local dest_filepath = to_filepath + src_filepath.after_first( from_folder )
        local ext = src_filepath.after_last( "." ).to_lowercase

        contingent
          necessary  (not File.is_folder(src_filepath))
          necessary  (ext == "png" or ext == "jpeg" or ext == "jpg")
          sufficient (not File.exists(dest_filepath))

          local dest_timestamp = File.timestamp( dest_filepath )
          local src_timestamp = File.timestamp( src_filepath )
          sufficient (src_timestamp > dest_timestamp)

          #sufficient (icom.file_timestamp > dest_timestamp)

          escapeContingent

        satisfied
          image_copy( src_filepath, dest_filepath, args )

        endContingent
      endForEach

    method image_copy( from_filepath:String, to_filepath:String, args:Value )
      local attributes = @{}
      forEach (attr in icom.attributes)
        if (File.matches_wildcard_pattern(from_filepath,attr//pattern))
          forEach (a in attr//attributes)
            if (a//value) attributes[ a//name ]         = a//value
            else          attributes[ a//name->String ] = true
          endForEach
        endIf
      endForEach

      println "Image Copy $ -> $" (from_filepath.after_first("Assets/Images/"),to_filepath.after_first("Assets/Images/"))

      local rescale = Convert.parse_scale( this, args//rescale )

      local resize = false
      local from_size : XY
      local to_size   : XY
      if (args//resize)
        local resize_dims = args//resize->String.split( "->" )
        if (resize_dims.count == 2)
          resize = true
          from_size = Convert.parse_dimensions( this, resize_dims.first )
          to_size   = Convert.parse_dimensions( this, resize_dims.last )
          if (not args//rescale)
            # Derive scale from resize args
            rescale = ((to_size.min / from_size.min) + (to_size.max / from_size.max)) / 2
          endIf
        endIf
      endIf

      if (rescale == 1.0)
        File.copy( from_filepath, to_filepath )
      else
        local bitmap = Bitmaps[ from_filepath ]
        local w = bitmap.width
        local h = bitmap.height

        if (attributes//varfont)
          local characters = Convert.extract_varfont_characters( this, bitmap )
          print "  # character".pluralized( characters.count )
          print " [$x$] -> " (w,h)
          forEach (ch in characters) ch.resize( ch.width*rescale, ch.height*rescale )
          bitmap = Convert.pack_varfont_characters( this, characters )
          println " [$x$]" (bitmap.width,bitmap.height)
          save_bitmap( bitmap, to_filepath )

        elseIf (attributes//split)
          local size = Convert.parse_dimensions( this, attributes//split )
          local tiles_wide = size//width->Int32
          local tiles_high = size//height->Int32
          if (tiles_wide * tiles_high == 0)
            throw error( ''Illegal 'split' size of $x$ for "$".'' (tiles_wide,tiles_high,from_filepath) )
          endIf
          local tiles = Convert.bitmap_to_tiles( this, bitmap, tiles_wide, tiles_high )
          print "  $x$ tiles" (tiles_wide,tiles_high)
          print " [$x$] -> " (w,h)
          forEach (tile in tiles) tile.resize( tile.width*rescale, tile.height*rescale )
          bitmap = Convert.tiles_to_bitmap( this, tiles, tiles_wide, tiles_high )
          println " [$x$]" (bitmap.width,bitmap.height)
          save_bitmap( bitmap, to_filepath )

        else
          local new_w = (w * rescale)->Int32
          local new_h = (h * rescale)->Int32
          if (resize)
            if (w == from_size.min and h == from_size.max)     new_w = to_size.min; new_h = to_size.max
            elseIf (w == from_size.max and h == from_size.min) new_w = to_size.max; new_h = to_size.min
          endIf
          bitmap = bitmap.resized( new_w, new_h )
          println "  [$x$] -> [$x$]" (w,h,bitmap.width,bitmap.height)

          save_bitmap( bitmap, to_filepath )
        endIf
      endIf

    method save_bitmap( bitmap:Bitmap, to_filepath:String )
      local is_png = true
      local lc = to_filepath.to_lowercase
      if (lc.ends_with(".jpg") or lc.ends_with(".jpeg")) is_png = false

      File.create_folder( File.folder(to_filepath) )
      if (is_png)
        File.save( to_filepath, bitmap.to_png_bytes )
      else
        File.save( to_filepath, bitmap.to_jpeg_bytes )
      endIf


    method must_consume( ch:Character )->Logical
      if (consume(ch)) return true

      if (reader.has_another)
        throw error( ''Syntax error - expected "$", found "$".'' (ch,reader.peek) )
      else
        throw error( ''Syntax error - expected "$", found [end of line].'' (ch) )
      endIf

    method must_consume( text:String )->Logical
      if (consume(text)) return true

      if (reader.has_another)
        throw error( ''Syntax error - expected "$", found "$".'' (text,reader.peek) )
      else
        throw error( ''Syntax error - expected "$", found [end of line].'' (text) )
      endIf

    method parse_args->@{}
      local args = @{}
      while (has_another)
        local arg = parse_name_value
        if (arg//value) args[ arg//name ]         = arg//value
        else            args[ arg//name->String ] = true
      endWhile
      return args

    method parse_name_value->Value
      local name = parse_string( "Identifier or [end of line] expected." )
      local name_value = @{ name:name }
      consume_whitespace
      if (consume(':'))
        name_value//value = parse_string( "Value expected." )
      endIf
      return name_value

    method parse_string( expected:String )->String
      if (not has_another) throw error( expected )
      buffer.clear
      local ch = reader.peek
      if (ch == '"' or ch == '\'')
        # Quote-delimited
        local quote_type = reader.read
        while (reader.has_another)
          ch = reader.read
          which (ch)
            case quote_type: return buffer->String
            case '\\'
              if (reader.has_another) buffer.print( reader.read )
            others
              buffer.print( ch )
          endWhich
        endWhile
        throw error( "String is missing closing ($)." (quote_type) )
      else
        while (reader.has_another)
          ch = reader.peek
          which (ch)
            case ' ', ':', ','
              escapeWhile
            others
              buffer.print reader.read
          endWhich
        endWhile
        if (buffer.count == 0) throw error( expected )
      endIf
      return buffer->String

    method require_arg( args:@{}, name:String  )
      if (not args.contains(name)) throw error( "Missing required argument '$'." (name) )

    method to->String
      return content
endClass

class Convert
  GLOBAL METHODS
    method bitmap_to_tiles( cmd:Command, bitmap:Bitmap, tiles_wide:Int32, tiles_high:Int32 )->Bitmap[]
      local tiles = Bitmap[]
      local w = bitmap.width / tiles_wide
      local h = bitmap.height / tiles_high
      forEach (j in 0..<tiles_high)
        forEach (i in 0..<tiles_wide)
          tiles.add( Bitmap(bitmap,Box(i*w,j*h,w,h)) )
        endForEach
      endForEach
      return tiles

    method tiles_to_bitmap( cmd:Command, tiles:Bitmap[], tiles_wide:Int32, tiles_high:Int32 )->Bitmap
      require (tiles.count == tiles_wide * tiles_high)
      local w = tiles.first.width
      local h = tiles.first.height
      local bitmap = Bitmap( w*tiles_wide, h*tiles_high )
      forEach (j in 0..<tiles_high)
        forEach (i in 0..<tiles_wide)
          tiles[ j*tiles_wide + i ].blit( bitmap, XY(i*w,j*h) )
        endForEach
      endForEach
      return bitmap

    method extract_varfont_characters( cmd:Command, bitmap:Bitmap )->Bitmap[]
      local characters = Bitmap[]
      local x1 = 0
      local w  = bitmap.width
      local char_height = bitmap.height - 1

      while (x1 < w)
        local color = bitmap.get( x1, 0 )
        local x_limit = x1 + 1
        while (x_limit < w and bitmap.get(x_limit,0) == color) ++x_limit
        characters.add( Bitmap(bitmap,Box(x1,1,x_limit-x1,char_height)) )
        x1 = x_limit
      endWhile

      return characters

    method pack_varfont_characters( cmd:Command, characters:Bitmap[] )->Bitmap
      require characters.count
      local total_w = 0
      total_w += (forEach in characters).width
      local bitmap = Bitmap( total_w, characters.first.height + 1 )
      local color = Color.RED
      local x = 0
      forEach (ch in characters)
        bitmap.fill( Box(x,0,ch.width,1), color )
        ch.blit( bitmap, XY(x,1) )
        if (color == Color.RED) color = 0xFF00FF00
        else                    color = Color.RED
        x += ch.width
      endForEach
      return bitmap

    method parse_dimensions( cmd:Command, text:String )->Value
      local dims = text.split( "x" )
      if (dims.count == 1)
        local size = dims.first->Int32
        return @{ width:size, height:size }
      else
        return @{ width:dims[0]->Int32, height:dims[1]->Int32 }
      endIf

    method parse_scale( cmd:Command, text:String )->Real64
      if (text.count == 0) return 1.0
      if (not text.contains('/')) return text->Real64
      local parts = text.split( "/" )
      return parts[0]->Real64 / parts[1]->Real64

endClass


class Asset
  PROPERTIES
    cmd          : Command
    filepath     : String
    attributes   : @{}
    placements   = Placement[]
    type         = "TextureImage"

  METHODS
    method init( cmd, filepath, attributes )
      local bitmap = Bitmaps[ filepath ]

      if (attributes//split)
        # Split into tiles
        type = "Image[]"
        local size = Convert.parse_dimensions( cmd, attributes//split )
        local tiles_wide = size//width->Int32
        local tiles_high = size//height->Int32
        if (tiles_wide * tiles_high == 0)
          throw cmd.error( ''Illegal 'split' size of $x$ for "$".'' (tiles_wide,tiles_high,filepath) )
        endIf
        local tiles = Convert.bitmap_to_tiles( cmd, bitmap, tiles_wide, tiles_high )
        placements.add( Placement( this, (forEach in tiles), attributes ) )

      elseIf (attributes//varfont)
        type = "Font"
        local characters = Convert.extract_varfont_characters( cmd, bitmap )
        placements.add( Placement( this, (forEach in characters), attributes ) )

      else
        # Standard image
        placements.add( Placement( this, bitmap, attributes ) )

      endIf

    method collect_image_info( image_list:@[] )
      local entry = @{ type:type, name:filepath.after_first("Assets/Images/") }

      which (type)
        case "TextureImage"
          placements.first.collect_image_info( entry )

        case "Image[]"
          local images = entry.ensure_list( "images" )
          images.add( (forEach in placements).collect_image_info( @{ type:"TextureImage" } ) )

        case "Font"
          local images = entry.ensure_list( "characters" )
          local characters : String
          if (attributes//varfont.is_string)
            characters = attributes//varfont->String
            if (characters.count != placements.count)
              throw cmd.error( ''Font strip "$" contains $ characters but the varfont character mapping specifies $.''...
                (filepath.after_first("Assets/Images/"),placements.count,characters.count) )
            endIf
          else
            local buffer = StringBuilder()
            buffer.print( (forEach in 32..126)->Character )
            characters = buffer->String

            if (characters.count != placements.count)
              throw cmd.error( ''Font strip "$" contains $ characters but the default varfont character mapping specifies 95.''...
                (filepath.after_first("Assets/Images/"),placements.count) )
            endIf
          endIf

          local unicode = characters.reader
          images.add( (forEach in placements).collect_image_info( @{ type:"TextureImage", unicode:unicode.read->Int32 } ) )

        others
          throw Error( "INTERNAL ERROR in Asset.collect_image_info(): Unhandled image type: " + type )
      endWhich

      image_list.add( entry )

    method collect_placements( list:Placement[] )
      list.add( placements )
endClass


class Placement
  PROPERTIES
    asset          : Asset
    bitmap         : Bitmap
    attributes     : @{}
    x              : Int32
    y              : Int32
    width          : Int32
    height         : Int32
    display_width  : Int32
    display_height : Int32
    image_sheet    : ImageSheet

  METHODS
    method init( asset, bitmap, attributes )
      width = bitmap.width
      height = bitmap.height
      display_width  = width
      display_height = height

      if (attributes//resample)
        local tx_size   = Convert.parse_dimensions( asset.cmd, attributes//resample )
        width  = tx_size//width->Int32
        height = tx_size//height->Int32
        bitmap = bitmap.resized( width, height )
      endIf

      local display_scale = asset.cmd.icom.settings//display_scale
      if (display_scale)
        local scale = Convert.parse_scale( asset.cmd, display_scale )
        display_width  = display_width * scale
        display_height = display_height * scale
      endIf

    method collect_image_info( info:@{} )->@{}
      info//display_size = @{ width:display_width, height:display_height }
      info//texture =
      @{
        filename:image_sheet.filename,
        bounds: { x:x, y:y, width:width, height:height }
      }
      return info

    method render( to_bitmap:Bitmap )
      local pos  = XY( x, y )
      local size = XY( bitmap.width, bitmap.height )

      bitmap.blit( to_bitmap, pos )

      # Extend each edge 1 pixel to prevent rendering artifacts
      to_bitmap.blit( Box(pos,size.x,1),                to_bitmap, pos - XY(0,1) )  # top
      to_bitmap.blit( Box(pos+XY(0,size.y-1),size.x,1), to_bitmap, pos + size.yv )  # bottom

      to_bitmap.blit( Box(pos-XY(0,1),1,size.y+2),         to_bitmap, pos-XY(1,1) )         # left
      to_bitmap.blit( Box(pos+size.xv-XY(1,1),1,size.y+2), to_bitmap, pos+size.xv-XY(0,1) ) # right
endClass


class ImageSheet
  PROPERTIES
    placements = Placement[]
    width      : Int32
    height     : Int32
    filename   : String

  METHODS
    method init( width, height )

    method add_placement( placement:Placement )
      placements.add( placement )
      placement.image_sheet = this

    method render->Bitmap
      local bitmap = Bitmap( width, height, Color.BLUE )
      (forEach in placements).render( bitmap )
      return bitmap

endClass

class CommentStripper
  PROPERTIES
    reader        : StringReader
    lines         = String[]
    buffer        = StringBuilder()
    comment_level : Int32

  METHODS
    method process( text:String )->String[]
      reader = text.reader
      while (reader.has_another) scan_next
      if (buffer.count) lines.add( buffer->String )
      return lines

    method scan_next
      local ch = reader.read
      which (ch)
        case '#'
          if (reader.peek == '{')
            # Multiline comment
            reader.read  # '{'
            ++comment_level
          else
            # Single line comment
            while (reader.has_another and reader.peek != '\n') reader.read
          endIf

        case '}'
          if (comment_level and reader.peek == '#')
            reader.read
            --comment_level
          else
            write( '}' )
          endIf

        case '"', '\''
          # "string"
          local quote_type = ch
          write( ch )
          while (reader.has_another)
            ch = reader.read
            write( ch )
            which (ch)
              case '\\':       write( reader.read ) # skip past next
              case quote_type: escapeWhile
            endWhich
          endWhile

        others
          write( ch )

      endWhich

    method write( ch:Character )->this
      if (ch == '\n')
        lines.add( buffer->String )
        buffer.clear
      elseIf (comment_level == 0)
        buffer.print( ch )
      endIf

      return this

endClass

