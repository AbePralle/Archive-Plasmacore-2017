$include "../Rogue/Plasmacore"
using Plasmacore

try
  local config_file = File( "Assets/Images/ImageConfig.txt" )
  if (not config_file.exists)
    File.create_folder( config_file.path )
    local writer = PrintWriter(config_file.writer)
    writer.println @|# Default config file - customize as desired.
                    |[Global]
                    |@ **/*Font* varfont
                    |@ **/*font* varfont
                    |
                    |[Default]
                    |imageSheetSize 2048
                    |group .
    writer.close
  endIf



  local args = System.command_line_arguments
  if (args.count)
    ImageCompiler( config_file, (forEach in args) )
  else
    ImageCompiler( config_file, "Default" )
  endIf

catch (err:Error)
  println err
  System.exit 1
endTry

class Bitmaps : Table<<String,Bitmap>> [singleton]
  METHODS
    method get( name:String )->Bitmap
      local bitmap = prior.get( name )
      if (bitmap) return bitmap

      bitmap = Bitmap( File(name) )
      this[ name ] = bitmap

      return bitmap
endClass

class BuildError( cmd:Command, message ) : Error
  METHODS
    method init( message )

    method to->String
      local builder = StringBuilder()

      local w = Console.width.or_smaller( 79 )
      local bar = "=" * w
      builder.println bar
      if (cmd)
        builder.println( (''IMAGE COMPILE ERROR in "$" line $'' (cmd.src_filepath,cmd.src_line)).word_wrapped(w) )
      else
        builder.println "IMAGE COMPILE ERROR"
      endIf
      builder.println
      builder.println message.word_wrapped( w )
      builder.println bar
      return builder->String
endClass

class ImageCompiler
  PROPERTIES
    file             : File
    file_timestamp   : Real64
    platform         : String
    commands         = Table<<String,Command[]>>()
    attributes       = @[]
    settings         = @{}
    image_sheet_size = 2048
    group_manifests  = @{}
    oldest_group_manifest_timestamp : Real64
    even_odd         = true

  METHODS
    method init( file, platform )
      println "Compiling images for platform " + platform
      file_timestamp = file.timestamp
      group_manifests = @{}
      oldest_group_manifest_timestamp = 0.0

      organize_commands
      execute_global_commands
      execute_platform_commands

    method execute_global_commands
      if (not commands.contains("Global")) return
      (forEach in commands//Global).execute

    method execute_platform_commands
      local working_platform = select{ commands.contains(platform):platform || "Default" }
      if (commands.contains(working_platform))
        (forEach in commands[working_platform]).execute
      else
        if (platform != "Default")
          println "IMAGE COMPILER WARNING: No configuration for platforms $ or Default in Assets/Images/ImageConfig.txt." (platform)
        else
          println "IMAGE COMPILER WARNING: No configuration for platform Default in Assets/Images/ImageConfig.txt."
        endIf
      endIf

    method organize_commands
      local category = "Global"
      forEach (line at index in LineReader(file))
        line = strip_comments( line.trimmed )
        if (line.count == 0) nextIteration
        if (line.begins_with('['))
          local after = line.after_first(']')
          if (after.count) throw BuildError( Command(this,file.filepath,index+1,line), "Syntax error after ']'." )
          category = line.after_first( '[' ).before_first( ']' ).trimmed
        else
          ensure commands[ category ]
          commands[ category ].add( Command(this,file.filepath,index+1,line) )
        endIf
      endForEach

    method strip_comments( text:String )->String
      if (not text.contains('#')) return text

      local in_string   = 0
      local ignore_next = false
      forEach (ch at index in text)
        if (ignore_next)
          ignore_next = false
        else
          if (in_string)
            which (ch)
              case '\\':      ignore_next = true
              case in_string: in_string = 0
            endWhich
          else
            which (ch)
              case '"', '\'': in_string = ch
              case '#': return text.leftmost( index )
            endWhich
          endIf
        endIf
      endForEach

      return text

endClass


class Command
  PROPERTIES
    icom         : ImageCompiler
    src_filepath : String
    src_line     : Int32
    content      : String
    reader       : ParseReader
    buffer       = StringBuilder()

  METHODS
    method init( icom, src_filepath, src_line, content )
      reader = ParseReader( content )

    method collect_files( filepath:String )->String[]
      local pattern = filepath
      if (File.exists(filepath))
        pattern = "**"
      else
        while (filepath.contains('*') or filepath.contains('?')) filepath = File.folder( filepath )
        if (not File.exists(filepath)) throw error( ''No such file or folder "$".'' (filepath) )
      endIf

      local file_list = String[]

      if (File.is_folder(filepath))
        forEach (f in File.listing(filepath,&ignore_hidden,&recursive))
          if (File.matches_wildcard_pattern(f,pattern)) file_list.add( f )
        endForEach
      else
        file_list.add( filepath )
      endIf

      local filtered_list = String[]
      forEach (f in file_list)
        if (not File.is_folder(f)) filtered_list.add( f )
      endForEach

      return filtered_list

    method consume( ch:Character )->Logical
      return (reader.consume(ch))

    method consume( text:String )->Logical
      return (reader.consume(text))

    method consume_whitespace->Logical
      local found_any = false

      while (reader.consume_spaces or reader.consume('\t'))
        found_any = true
      endWhile

      return found_any

    method error( message:String )->BuildError
      return BuildError( this, message )

    method execute
      if (consume("@"))
        local pattern = parse_string( "Filename or file pattern expected after \"@\"." )
        local info = @{ pattern:pattern, attributes:[] }
        local characters : StringBuilder
        while (has_another)
          local name = parse_string( "Attribute or [end of line] expected." )
          consume_whitespace
          if (consume(':'))
            local value = parse_string( "Value expected." )
            if (name == "varfont")
              ensure characters
              characters.print( value )
              local attribute = @{ name:name }
              info//attributes.add( attribute )
            elseIf (name == "characters")
              ensure characters
              characters.print( value )
            elseIf (name == "unicode")
              ensure characters
              if (value.begins_with('['))
                local ranges = value.after_first('[').before_last(']').replacing(',',' ').split(' ')
                forEach (range in ranges)
                  if (range.contains('-'))
                    local parts = range.split( '-' )
                    characters.print( parts.first->Int32->Character )
                    characters.print( parts.last->Int32->Character )
                  else
                    characters.print( range->Int32->Character )
                  endIf
                endForEach
              else
                characters.print( value->Int32->Character )
              endIf
            else
              local attribute = @{ name:name }
              attribute//value = value
              info//attributes.add( attribute )
            endIf
          else
            local attribute = @{ name:name }
            info//attributes.add( attribute )
          endIf
        endWhile
        if (characters)
          forEach (attr in info//attributes)
            if (attr//name == "varfont") attr//value = characters->String
          endForEach
        endIf
        icom.attributes.add( info )
        return
      endIf

      local cmd = parse_string( "Command expected." )
      which (cmd)
        case "group"
          create_group

        case "image_copy"
          local args = parse_args
          require_arg( args, "from" )
          require_arg( args, "to" )
          image_copy( args )

        case "set"
          local setting = parse_name_value
          icom.settings[ setting//name ] = setting//value

        others
          trace "TODO ", cmd
      endWhich

    method create_group
      local group_name = parse_string( "Folder or file pattern expected." )
      if (group_name == ".")
        create_group( "Images" )
      elseIf (group_name.contains('?') or group_name.contains('*'))
        forEach (filepath in File.listing("Assets/Images/"+group_name,&ignore_hidden))
          create_group( filepath.after_first("Assets/") )
        endForEach
      else
        create_group( "Images/" + group_name )
      endIf

    method create_group( group_name:String )
      local pattern = group_name
      group_name = File.before_wildcard_pattern( pattern )
      local output_filename = group_name.replacing('/','-')
      pattern = "Assets/" + pattern

      local dest_folder = "Build/$/Assets/Images" (icom.platform)
      File.create_folder( dest_folder )

      local oldest_existing_timestamp = 0.0
      forEach (filepath in File.listing("$/$*.png" (dest_folder,output_filename)))
        local timestamp = File.timestamp( filepath )
        if (oldest_existing_timestamp == 0.0 or oldest_existing_timestamp > timestamp)
          oldest_existing_timestamp = timestamp
        endIf
      endForEach

      local manifest_folder = "Build/$/Intermediate/ImageGroupManifests" (icom.platform)
      File.create_folder( manifest_folder )
      local manifest_filepath = "$/$.json" (manifest_folder,output_filename)
      local manifest_timestamp = 0.0
      if (File.exists(manifest_filepath)) manifest_timestamp = File.timestamp( manifest_filepath )
      oldest_existing_timestamp = oldest_existing_timestamp.or_smaller( manifest_timestamp )

      #local args = parse_args
      local files = collect_files( pattern )

      contingent
        necessary (icom.file_timestamp < oldest_existing_timestamp)
        forEach (filepath in files)
          necessary (File.timestamp(filepath) < oldest_existing_timestamp)
        endForEach

        # No changes to group.  Load up cached group manifest so it can be written out
        # with the platform manifest
        icom.group_manifests[ group_name ] = JSON.load_table( manifest_filepath )
        if (icom.oldest_group_manifest_timestamp == 0.0 or manifest_timestamp < icom.oldest_group_manifest_timestamp)
          icom.oldest_group_manifest_timestamp = manifest_timestamp
        endIf

        return  # no changes to group
      endContingent

      local assets = Asset[]
      forEach (filepath in files)
        local ext = File.extension(filepath).to_lowercase

        if (ext == "png" or ext == "jpeg" or ext == "jpg")
          assets.add( create_asset_object(filepath) )
        endIf
      endForEach

      if (not assets.count)
        println "Skipping empty group " + group_name
        return
      endIf

      local image_sheets = perform_layout( assets )

      # Build group manifest
      local manifest = @{ name:group_name }

      local textures = manifest.ensure_list( "textures" )
      forEach (sheet at index in image_sheets)
        sheet.filename = "$-$.png" (output_filename,index.right_justified(2,'0'))
        print "  "
        print sheet.filename
        print " "
        if (icom.even_odd)
          forEach (1..(50 - sheet.filename.count)) print "."
        else
          forEach (1..(50 - sheet.filename.count)) print " "
        endIf
        icom.even_odd = not icom.even_odd
        println " [$x$]" (sheet.width,sheet.height)

        textures.add(
        @{
          filename: sheet.filename,
          size: { width:sheet.width, height:sheet.height }
        } )

      endForEach

      local images = manifest.ensure_list( "images" )
      forEach (asset in assets)
        asset.collect_image_info( images )
      endForEach

      # Store group manifest
      icom.group_manifests[ group_name ] = manifest
      manifest_timestamp = System.time
      if (icom.oldest_group_manifest_timestamp == 0.0 or manifest_timestamp < icom.oldest_group_manifest_timestamp)
        icom.oldest_group_manifest_timestamp = manifest_timestamp
      endIf

      manifest.save( File(manifest_filepath), &formatted )


    method perform_layout( assets:Asset[] )->ImageSheet[]
      local placements = Placement[]
      (forEach in assets).collect_placements( placements )

      local max_w = select{ icom.settings//image_sheet_size->Int32 || 2048 }.to_power_of_two
      local max_h = max_w

      local sheets = ImageSheet[]
      while (placements.count)
        local sheet = perform_layout( placements, max_w, max_h )
        if (not sheet)
          local w = placements.first.width
          local h = placements.first.height
          local min_size = w.to_power_of_two.or_larger( h.to_power_of_two )
          throw error( "$ ($x$) is too large to fit on a $x$ image sheet.  Use the 'set image_sheet_size WxH' directive " ...
                  "to increase the group's maximum image sheet size to $x$ or greater." ...
                  (placements.first.asset.filepath,w,h,max_w,max_h,min_size,min_size) )
          return null
        endIf
        sheets.add( sheet )
      endWhile

      require sheets.count

      # Try reducing the size of the last sheet by redoing the layout
      local last_sheet = sheets.remove_last

      loop
        placements.clear.add( last_sheet.placements )
        local better_fit = perform_layout( placements, last_sheet.width, last_sheet.height/2 )
        if (better_fit and placements.count == 0)
          last_sheet = better_fit
          nextIteration
        endIf

        placements.clear.add( last_sheet.placements )
        better_fit = perform_layout( placements, last_sheet.width/2, last_sheet.height )
        if (better_fit and placements.count == 0)
          last_sheet = better_fit
          nextIteration
        endIf

        escapeLoop
      endLoop

      # Redo the last sheet layout one final time with the best dimensions - any failed
      # attempts above have left the images positioned badly.
      placements.clear.add( last_sheet.placements )
      last_sheet = perform_layout( placements, last_sheet.width, last_sheet.height )

      sheets.add( last_sheet )

      return sheets

    method perform_layout( placements:Placement[], max_width:Int32, max_height:Int32 )->ImageSheet
      InsertionSort<<Placement>>.sort( placements,
        function(a:Placement,b:Placement)->(Logical)
          # 'a' should come before 'b' when:
          if (a.height > b.height) return true
          if (a.height < b.height) return false
          # Heights are equal

          return (a.width > b.width)
        endFunction
      )

      local sheet = ImageSheet( max_width, max_height )
      if (perform_layout(placements, -1, -1, max_width+2, max_height+2, sheet)) return sheet
      else return null

    method perform_layout( placements:Placement[], sx:Int32, sy:Int32, sw:Int32, sh:Int32, image_sheet:ImageSheet )->Logical
      # Find the largest asset we can place (including a 1-pixel border around
      # the edge), delete it from the list, and then recursively place any
      # remaining placements we can.
      forEach (placement in placements)
        local w = (placement.width  + 2)->Int32
        local h = (placement.height + 2)->Int32
        if (w <= sw and h <= sh)
          placement.x = sx+1
          placement.y = sy+1
          placements.remove( placement )
          image_sheet.add_placement( placement )

          # We've just placed 'A', now recursively fill areas 'B' and 'C':
          #
          # AAA|BBBBBB
          # AAA|BBBBBB
          # ---+------
          # CCCCCCCCCC
          # CCCCCCCCCC
          # CCCCCCCCCC
          perform_layout( placements, sx+w, sy,   sw-w, h,    image_sheet )   # B
          perform_layout( placements, sx,   sy+h, sw,   sh-h, image_sheet )   # C

          return true
        endIf
      endForEach

      # Unable to place any
      return false

    method create_asset_object( filepath:String )->Asset
      local attributes = @{}
      forEach (attr in icom.attributes)
        if (File.matches_wildcard_pattern(filepath,attr//pattern))
          forEach (a in attr//attributes)
            if (a//value) attributes[ a//name ]         = a//value
            else          attributes[ a//name->String ] = true
          endForEach
        endIf
      endForEach

      return Asset( this, filepath, attributes )

    method has_another->Logical
      consume_whitespace
      return (reader.has_another)

    method image_copy( args:@{} )
      require_arg( args, "from" )
      require_arg( args, "to" )
      local from_filepath = args//from->String
      local to_filepath   = args//to->String
      if (not from_filepath.begins_with("Assets/Images/")) from_filepath = "Assets/Images/" + from_filepath
      if (not to_filepath.begins_with("Assets/Images/"))   to_filepath = "Assets/Images/" + to_filepath
      local files = collect_files( from_filepath )
      forEach (src_filepath in files)
        local from_folder = File.before_wildcard_pattern( from_filepath )
        local dest_filepath = to_filepath + src_filepath.after_first( from_folder )
        local ext = src_filepath.after_last( "." ).to_lowercase

        contingent
          necessary  (not File.is_folder(src_filepath))
          necessary  (ext == "png" or ext == "jpeg" or ext == "jpg")
          sufficient (not File.exists(dest_filepath))

          local dest_timestamp = File.timestamp( dest_filepath )
          local src_timestamp = File.timestamp( src_filepath )
          sufficient (src_timestamp > dest_timestamp)

          #sufficient (icom.file_timestamp > dest_timestamp)

          escapeContingent

        satisfied
          image_copy( src_filepath, dest_filepath, args )

        endContingent
      endForEach

    method image_copy( from_filepath:String, to_filepath:String, args:Value )
      local attributes = @{}
      forEach (attr in icom.attributes)
        if (File.matches_wildcard_pattern(from_filepath,attr//pattern))
          forEach (a in attr//attributes)
            if (a//value) attributes[ a//name ]         = a//value
            else          attributes[ a//name->String ] = true
          endForEach
        endIf
      endForEach

      println "Image Copy $ -> $" (from_filepath.after_first("Assets/Images/"),to_filepath.after_first("Assets/Images/"))

      local scale = Convert.parse_scale( args//rescale )
      local resize_ipad = (args//resize == "ipad_pro_to_retina")
      if (resize_ipad) scale = 0.75

      if (scale == 1.0)
        File.copy( from_filepath, to_filepath )
      else
        local bitmap = Bitmaps[ from_filepath ]
        local w = bitmap.width
        local h = bitmap.height

        if (attributes//varfont)
          local characters = Convert.extract_varfont_characters( bitmap )
          print "  # character".pluralized( characters.count )
          print " [$x$] -> " (w,h)
          forEach (ch in characters) ch.resize( ch.width*scale, ch.height*scale )
          bitmap = Convert.pack_varfont_characters( characters )
          println " [$x$]" (bitmap.width,bitmap.height)
          save_bitmap( bitmap, to_filepath )

        elseIf (attributes//split)
          local size = Convert.parse_dimensions( attributes//split )
          local tiles_wide = size//width->Int32
          local tiles_high = size//height->Int32
          local tiles = Convert.bitmap_to_tiles( bitmap, tiles_wide, tiles_high )
          print "  $x$ tiles" (tiles_wide,tiles_high)
          print " [$x$] -> " (w,h)
          forEach (tile in tiles) tile.resize( tile.width*scale, tile.height*scale )
          bitmap = Convert.tiles_to_bitmap( tiles, tiles_wide, tiles_high )
          println " [$x$]" (bitmap.width,bitmap.height)
          save_bitmap( bitmap, to_filepath )

        else
          local new_w = (w * scale)->Int32
          local new_h = (h * scale)->Int32
          if (resize_ipad)
            if (w == 2732 and h == 2048)     new_w = 2048; new_h = 1536
            elseIf (w == 2048 and h == 2732) new_w = 1536; new_h = 2048
          endIf
          bitmap.resize( new_w, new_h )
          println "  [$x$] -> [$x$]" (w,h,bitmap.width,bitmap.height)

          save_bitmap( bitmap, to_filepath )
        endIf
      endIf

    method save_bitmap( bitmap:Bitmap, to_filepath:String )
      local is_png = true
      local lc = to_filepath.to_lowercase
      if (lc.ends_with(".jpg") or lc.ends_with(".jpeg")) is_png = false

      File.create_folder( File.folder(to_filepath) )
      if (is_png)
        File.save( to_filepath, bitmap.to_png_bytes )
      else
        File.save( to_filepath, bitmap.to_jpeg_bytes )
      endIf


    method must_consume( ch:Character )->Logical
      if (consume(ch)) return true

      if (reader.has_another)
        throw error( ''Syntax error - expected "$", found "$".'' (ch,reader.peek) )
      else
        throw error( ''Syntax error - expected "$", found [end of line].'' (ch) )
      endIf

    method must_consume( text:String )->Logical
      if (consume(text)) return true

      if (reader.has_another)
        throw error( ''Syntax error - expected "$", found "$".'' (text,reader.peek) )
      else
        throw error( ''Syntax error - expected "$", found [end of line].'' (text) )
      endIf

    method parse_args->@{}
      local args = @{}
      while (has_another)
        local arg = parse_name_value
        if (arg//value) args[ arg//name ]         = arg//value
        else            args[ arg//name->String ] = true
      endWhile
      return args

    method parse_name_value->Value
      local name = parse_string( "Identifier or [end of line] expected." )
      local name_value = @{ name:name }
      consume_whitespace
      if (consume(':'))
        name_value//value = parse_string( "Value expected." )
      endIf
      return name_value

    method parse_string( expected:String )->String
      if (not has_another) throw error( expected )
      buffer.clear
      local ch = reader.peek
      if (ch == '"' or ch == '\'')
        # Quote-delimited
        local quote_type = reader.read
        while (reader.has_another)
          ch = reader.read
          which (ch)
            case quote_type: return buffer->String
            case '\\'
              if (reader.has_another) buffer.print( reader.read )
            others
              buffer.print( ch )
          endWhich
        endWhile
        throw error( "String is missing closing ($)." (quote_type) )
      else
        while (reader.has_another)
          ch = reader.peek
          which (ch)
            case ' ', ':', ','
              escapeWhile
            others
              buffer.print reader.read
          endWhich
        endWhile
        if (buffer.count == 0) throw error( expected )
      endIf
      return buffer->String

    method require_arg( args:@{}, name:String  )
      if (not args.contains(name)) throw error( "Missing required argument '$'." (name) )

    method to->String
      return content
endClass

class Convert
  GLOBAL METHODS
    method bitmap_to_tiles( bitmap:Bitmap, tiles_wide:Int32, tiles_high:Int32 )->Bitmap[]
      local tiles = Bitmap[]
      local w = bitmap.width / tiles_wide
      local h = bitmap.height / tiles_high
      forEach (j in 0..<tiles_high)
        forEach (i in 0..<tiles_wide)
          tiles.add( Bitmap(bitmap,Box(i*w,j*h,w,h)) )
        endForEach
      endForEach
      return tiles

    method tiles_to_bitmap( tiles:Bitmap[], tiles_wide:Int32, tiles_high:Int32 )->Bitmap
      require (tiles.count == tiles_wide * tiles_high)
      local w = tiles.first.width
      local h = tiles.first.height
      local bitmap = Bitmap( w*tiles_wide, h*tiles_high )
      forEach (j in 0..<tiles_high)
        forEach (i in 0..<tiles_wide)
          tiles[ j*tiles_wide + i ].blit( bitmap, XY(i*w,j*h) )
        endForEach
      endForEach
      return bitmap

    method extract_varfont_characters( bitmap:Bitmap )->Bitmap[]
      local characters = Bitmap[]
      local x1 = 0
      local w  = bitmap.width
      local char_height = bitmap.height - 1

      while (x1 < w)
        local color = bitmap.get( x1, 0 )
        local x_limit = x1 + 1
        while (x_limit < w and bitmap.get(x_limit,0) == color) ++x_limit
        characters.add( Bitmap(bitmap,Box(x1,1,x_limit-x1,char_height)) )
        x1 = x_limit
      endWhile

      return characters

    method pack_varfont_characters( characters:Bitmap[] )->Bitmap
      require characters.count
      local total_w = 0
      total_w += (forEach in characters).width
      local bitmap = Bitmap( total_w, characters.first.height + 1 )
      local color = Color.RED
      local x = 0
      forEach (ch in characters)
        bitmap.fill( Box(x,0,ch.width,1), color )
        ch.blit( bitmap, XY(x,1) )
        if (color == Color.RED) color = 0xFF00FF00
        else                    color = Color.RED
        x += ch.width
      endForEach
      return bitmap

    method parse_dimensions( text:String )->Value
      local dims = text.split( "x" )
      if (dims.count == 1)
        local size = dims.first->Int32
        return @{ width:size, height:size }
      else
        return @{ width:dims[0]->Int32, height:dims[1]->Int32 }
      endIf

    method parse_scale( text:String )->Real64
      if (text.count == 0) return 1.0
      if (not text.contains('/')) return text->Real64
      local parts = text.split( "/" )
      return parts[0]->Real64 / parts[1]->Real64

endClass


class Asset
  PROPERTIES
    cmd          : Command
    filepath     : String
    attributes   : @{}
    placements   = Placement[]
    type         = "TextureImage"

  METHODS
    method init( cmd, filepath, attributes )
      local bitmap = Bitmaps[ filepath ]

      if (attributes//split)
        # Split into tiles
        type = "Image[]"
        local size = Convert.parse_dimensions( attributes//split )
        local tiles_wide = size//width->Int32
        local tiles_high = size//height->Int32
        local tiles = Convert.bitmap_to_tiles( bitmap, tiles_wide, tiles_high )
        placements.add( Placement( this, (forEach in tiles) ) )

      elseIf (attributes//varfont)
        type = "Font"
        local characters = Convert.extract_varfont_characters( bitmap )
        placements.add( Placement( this, (forEach in characters) ) )

      else
        # Standard image
        placements.add( Placement( this, bitmap ) )

      endIf

    method collect_image_info( image_list:@[] )
      local entry = @{ type:type, name:filepath.after_first("Assets/Images/") }

      which (type)
        case "TextureImage"
          placements.first.collect_image_info( entry )

        case "Image[]"
          local images = entry.ensure_list( "images" )
          images.add( (forEach in placements).collect_image_info( @{ type:"TextureImage" } ) )

        case "Font"
          local images = entry.ensure_list( "characters" )
          local characters : String
          if (attributes//varfont.is_string)
            characters = attributes//varfont->String
            if (characters.count != placements.count)
              throw cmd.error( ''Font strip "$" contains $ characters but the varfont character mapping specifies $.''...
                (filepath.after_first("Assets/Images/"),placements.count,characters.count) )
            endIf
          else
            local buffer = StringBuilder()
            buffer.print( (forEach in 32..126)->Character )
            characters = buffer->String

            if (characters.count != placements.count)
              throw cmd.error( ''Font strip "$" contains $ characters but the default varfont character mapping specifies 95.''...
                (filepath.after_first("Assets/Images/"),placements.count) )
            endIf
          endIf

          local unicode = characters.reader
          images.add( (forEach in placements).collect_image_info( @{ type:"TextureImage", unicode:unicode.read->Int32 } ) )

        others
          throw Error( "INTERNAL ERROR in Asset.collect_image_info(): Unhandled image type: " + type )
      endWhich

      image_list.add( entry )

    method collect_placements( list:Placement[] )
      list.add( placements )
endClass


class Placement
  PROPERTIES
    asset          : Asset
    bitmap         : Bitmap
    x              : Int32
    y              : Int32
    width          : Int32
    height         : Int32
    display_width  : Int32
    display_height : Int32
    image_sheet    : ImageSheet

  METHODS
    method init( asset, bitmap )
      width = bitmap.width
      height = bitmap.height
      display_width  = width
      display_height = height

    method collect_image_info( info:@{} )->@{}
      info//display_size = @{ width:display_width, height:display_height }
      info//texture =
      @{
        filename:image_sheet.filename,
        bounds: { x:x, y:y, width:width, height:height }
      }
      return info
endClass


class ImageSheet
  PROPERTIES
    placements = Placement[]
    width      : Int32
    height     : Int32
    filename   : String

  METHODS
    method init( width, height )

    method add_placement( placement:Placement )
      placements.add( placement )
      placement.image_sheet = this

endClass

