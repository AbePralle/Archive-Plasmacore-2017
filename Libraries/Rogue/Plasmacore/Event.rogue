module Plasmacore

class Event
  PROPERTIES
    is_consumed = false

  METHODS
    method consume
      this.is_consumed = true
endClass

class KeyEvent : Event
  PROPERTIES
    keycode     : Int32
    syscode     : Int32
    is_press    : Logical
    is_repeat   : Logical

  METHODS
    method init( keycode, syscode, is_press=false, is_repeat=false )

    method is_press( _keycode:Int32 )->Logical
      return keycode == _keycode and is_press

    method is_repeat( _keycode:Int32 )->Logical
      return keycode == _keycode and is_repeat

    method is_release->Logical [macro]
      not this.is_press

    method is_release( _keycode:Int32 )->Logical
      return keycode == _keycode and not is_press

    method to->String
      local buffer = StringBuilder()
      which (keycode)
         case    1: buffer.print( "[LeftArrow] " )
         case    2: buffer.print( "[UpArrow] " )
         case    3: buffer.print( "[RightArrow] " )
         case    4: buffer.print( "[DownArrow] " )
         case    8: buffer.print( "[Backspace] " )
         case '\t': buffer.print( "[Tab] " )
         case '\n': buffer.print( "[Enter] " )
         case   27: buffer.print( "[Escape] " )
         case   32: buffer.print( "[Space] " )
         case  127: buffer.print( "[Delete] " )
         others
           buffer.print( "(Keycode " ).print( Int32(keycode) ).print( ") " )
       endWhich

       if (is_press)
         if (is_repeat) buffer.print( "pressed (repeating)" )
         else           buffer.print( "pressed" )
       else
         buffer.print( "released" )
       endIf

        if (is_consumed) buffer.print( " (consumed)" )

       return buffer->String

endClass


class TextEvent : Event
  PROPERTIES
    character : Character
    text      : String

  METHODS
    method init( character, text )

    method text->String
      if (not @text) text = "" + character
      return @text

    method count->Int32
      if (@text) return @text.count
      else       return 1

    method operator==( ch:Character )->Logical
      return (this.character == ch and (@text is null or @text.count == 1))

    method operator==( value:String )->Logical
      which (value.count)
        case 0
          return false
        case 1
          return (character == value[0])
        case 2
          return (text == value)
      endWhich

    method to->String
      return text
endClass


class PointerEvent : Event
  ENUMERATE
    MOVE     = 0
    PRESS    = 1
    RELEASE  = 2
    SCROLL   = 3
    # A PointerEvent never has type SCROLL; this definition is used internally to spin off
    # scroll events into their own ScrollEvent when they come in

  PROPERTIES
    type        : Int32
    position    : XY
    delta       : XY
    index       : Int32
    id          : Int32

  METHODS
    method init( type, position, delta, index, id )

    method is_move->Logical [macro]
      (this.type == PointerEvent.MOVE)

    method is_press->Logical [macro]
      (this.type == PointerEvent.PRESS)

    method is_press( button_index:Int32 )->Logical
      return (this.type == PRESS and index == button_index)

    method is_release->Logical [macro]
      (this.type == PointerEvent.RELEASE)

    method is_release( button_index:Int32 )->Logical
      return (this.type == RELEASE and index == button_index)

    method operator+( offset:XY )->PointerEvent
      return PointerEvent( type, position+offset, delta, index, id )

    method operator-( offset:XY )->PointerEvent
      return this + (-offset)

    method to->String
      local buffer = StringBuilder()
      buffer.print( "Pointer " )
      which (type)
        case MOVE:     buffer.print( "move" )
        case PRESS:    buffer.print( "press index " ).print( index )
        case RELEASE:  buffer.print( "release" )
      endWhich
      buffer.print( " at " ).print( position )
      if (is_consumed) buffer.print( " (consumed)" )

      return buffer->String
endClass


class ScrollEvent : Event
  PROPERTIES
    delta       : XY
    index       : Int32
    id          : Int32

  METHODS
    method init( delta, index, id )

    method to->String
      local buffer = StringBuilder().print( "Scroll " ).print( delta )
      if (is_consumed) buffer.print( " (consumed)" )
      return buffer->String
endClass

