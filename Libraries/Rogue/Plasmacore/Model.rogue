module Plasmacore

class Model
  PROPERTIES
    mesh        : Mesh
    renderer    : Renderer

    position    = XYZ(0,0,0)
    scale       = XYZ(1,1,1)
    rotation    = Quaternion()

  METHODS
    method init( mesh, renderer=Renderer.standard )

    method angles->Angles
      return @rotation->Angles

    method draw
      if (renderer)
        Display.push_world_transform( Matrix.translate(position) * rotation->Matrix * Matrix.scale(scale) )
        renderer.draw( this )
        Display.pop_world_transform
      endIf

    method set_angles( angles:Angles )->this
      @rotation = angles->Quaternion
      return this

    method set_position( @position )->this
      return this

    method set_position( xy:XY )->this
      @position = XYZ( xy, Display.z )
      return this

    method set_rotation( @rotation )->this
      return this

    method set_rotation( angles:Angles )->this
      @rotation = angles->Quaternion
      return this

    method update
endClass
