class Console : Reader<<Character>>, PrintWriter<<output_buffer>> [singleton]
  DEPENDENCIES
    nativeHeader #include <termios.h>
    nativeHeader #include <unistd.h>

  ENUMERATE
    UP_ARROW    = 17
    DOWN_ARROW  = 18
    RIGHT_ARROW = 19
    LEFT_ARROW  = 20

  GLOBAL METHODS
    method input( prompt=null:String )->String
      if (prompt) Global.print( prompt ).flush

      native @|char st[4096];
              |if (!fgets( st, 4096, stdin ))
              |{
              |  return RogueString_create_from_utf8( st, 0 );
              |}
              |
              |// discard \n
              |int len = strlen( st );
              |if (len) st[--len] = 0;
              |else st[0] = 0;
              |
              |return RogueString_create_from_utf8( st, len );

  PROPERTIES
    error             = ConsoleErrorPrinter()

    immediate_mode       = false
    # Setting to true/false automatically sets is_blocking to false/true

    is_blocking          = true

    decode_bytes         = true

    output_buffer        = StringBuilder()
    input_buffer         = StringBuilder()

    next_input_character : Int32?
    input_bytes          = Byte[]

    native "termios original_terminal_settings;"
    native "int     original_stdin_flags;"

  METHODS
    method init
      native @|tcgetattr( STDIN_FILENO, &$this->original_terminal_settings );
              |$this->original_stdin_flags = fcntl( STDIN_FILENO, F_GETFL );

      on_exit( function with (console=this) => console.reset_input_mode )

    method clear
      print( "\e[2J" ).flush

    method clear_to_eol
      print( "\e[K" ).flush

    method fill_input_queue
      # Internal use
      local n : Int32
      native @|char bytes[8];
              |$n = (RogueInt32) read( STDIN_FILENO, &bytes, 8 );
      if (n > 0)
        forEach (i in 0..<n)
          input_bytes.add( native("((RogueByte)bytes[$i])")->Byte )
        endForEach
      endIf

    method has_another->Logical
      if (not next_input_character.exists)
        fill_input_queue
        if (input_bytes.count)
          local b1 = input_bytes.remove_first
          contingent
            necessary (decode_bytes)
            necessary (input_bytes.count)  # more available?

            # More available
            if (b1 == 27)
              # Escape sequence
              necessary (input_bytes.count >= 2 and input_bytes.first == 91)
              input_bytes.remove_first
              next_input_character = (input_bytes.remove_first - 65) + Console.UP_ARROW

            else
              necessary (b1 >= 0xC0)

              local result : Int32

              # UTF-8
              if ((b1 & 0b1110_0000) == 0b1100_0000)
                necessary (input_bytes.count >= 1)
                result = b1 & 0b0001_1111
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
              elseIf ((b1 & 0b1111_0000) == 0b1110_0000)
                necessary (input_bytes.count >= 2)
                result = b1 & 0b0000_1111
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
              elseIf ((b1 & 0b1111_1000) == 0b1111_0000)
                necessary (input_bytes.count >= 3)
                result = b1 & 0b0000_0111
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
              elseIf ((b1 & 0b1111_1100) == 0b1111_1000)
                necessary (input_bytes.count >= 4)
                result = b1 & 0b0000_0011
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
              else
                necessary (input_bytes.count >= 5)
                result = b1 & 0b0000_0001
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
                result = (result :<<: 6) | (input_bytes.remove_first & 0b11_1111)
              endIf

              next_input_character = result
            endIf

          unsatisfied
            next_input_character = b1

          endContingent

          if (input_bytes.count > 0 and input_bytes.count < 6)
            # If there are more bytes left then characters are getting piped or redirected in.
            # Read enough to make sure we have a full UTF-8 sequence for next time.
            fill_input_queue
          endIf

        endIf
      endIf
      return next_input_character.exists

    method height->Int32
      nativeHeader #include <sys/ioctl.h>
      nativeHeader #include <unistd.h>

      native @|struct winsize sz;
              |ioctl( STDOUT_FILENO, TIOCGWINSZ, &sz );
              |
              |return sz.ws_row;

    method move_cursor( dx:Int32, dy:Int32 )
      if (dx)
        if (dx > 0) print( "\e[" ).print(  dx ).print( 'C' )
        else        print( "\e[" ).print( -dx ).print( 'D' )
      endIf
      if (dy)
        if (dy > 0) print( "\e[" ).print(  dy ).print( 'B' )
        else        print( "\e[" ).print( -dy ).print( 'A' )
      endIf
      Global.flush

    method peek->Character
      if (has_another) return next_input_character.value
      else             return 0

    method read->Character
      if (not has_another) return 0
      local result = next_input_character.value
      next_input_character = null
      return result

    method read_line->String
      input_buffer.clear
      forEach (byte in this)
        if (byte == 10) escapeForEach
        input_buffer.write( byte )
      endForEach
      return input_buffer->String

    method read_line( prompt:String )->String
      write prompt
      return read_line

    method read_line( prompt:StringBuilder )->String
      write prompt
      return read_line

    method reset_input_mode
      native @|tcsetattr( STDIN_FILENO, TCSANOW, &$this->original_terminal_settings );
              |fcntl( STDIN_FILENO, F_SETFL, $this->original_stdin_flags );

    method restore_cursor_position
      print( "\e[u" ).flush

    method set_is_blocking( setting:Logical )->this
      if (@is_blocking != setting)
        @is_blocking = setting
        if (@is_blocking)
          native @|fcntl( STDIN_FILENO, F_SETFL, O_NONBLOCK );
        else
          native @|fcntl( STDIN_FILENO, F_SETFL, $this->original_stdin_flags );
        endIf
      endIf
      return this

    method save_cursor_position
      print( "\e[s" ).flush

    method set_cursor( x:Int32, y:Int32 )
      print( "\e[" ).print( y ).print( ';' ).print( x ).print( 'H' ).flush

    method set_immediate_mode( setting:Logical )->this
      if (@immediate_mode != setting)
        @immediate_mode = setting
        if (@immediate_mode)
          # Turn off blocking and line buffering
          is_blocking = false
          native @|termios new_settings;
                  |tcgetattr( STDIN_FILENO, &new_settings );
                  |new_settings.c_lflag &= ~(ECHO | ECHOE | ICANON);
                  |new_settings.c_cc[VMIN] = 0;
                  |new_settings.c_cc[VTIME] = 0;
                  |tcsetattr( STDIN_FILENO, TCSANOW, &new_settings );
        else
          is_blocking = true
          native @|tcsetattr( STDIN_FILENO, TCSANOW, &$this->original_terminal_settings );
        endIf
      endIf
      return this

    method width->Int32
      nativeHeader #include <sys/ioctl.h>
      nativeHeader #include <unistd.h>

      native @|struct winsize sz;
              |ioctl( STDOUT_FILENO, TIOCGWINSZ, &sz );
              |
              |return sz.ws_col;

    method write( value:String )->this
      native @|fwrite( $value->utf8, 1, $value->byte_count, stdout );
              |fflush( stdout );
      return this

    method write( buffer:StringBuilder )->this
      native @|fwrite( $buffer->utf8->data->as_bytes, 1, $buffer->utf8->count, stdout );
              |fflush( stdout );
      return this

endClass

class ConsoleErrorPrinter : PrintWriter<<output_buffer>>
  PROPERTIES
    output_buffer = StringBuilder()

  METHODS
    method write( value:String )->this
      native @|fwrite( $value->utf8, 1, $value->byte_count, stderr );
              |fflush( stderr );
      return this

    method write( buffer:StringBuilder )->this
      native @|fwrite( $buffer->utf8->data->as_bytes, 1, $buffer->utf8->count, stderr );
              |fflush( stderr );
      return this

endClass

