class PrintWriter [aspect]
  GLOBAL METHODS
    method create( writer:Writer<<Byte>> )->PrintWriter
      return PrintWriterAdapter( writer )

  METHODS
    method close->this                                         [abstract]
    method flush->this                                         [abstract]
    method print( value:Byte )->this                           [abstract]
    method print( value:Character )->this                      [abstract]
    method print( value:Int32 )->this                          [abstract]
    method print( value:Logical )->this                        [abstract]
    method print( value:Int64 )->this                          [abstract]
    method print( value:Object )->this                         [abstract]
    method print( value:Real64 )->this                         [abstract]
    method print( value:Real64, decimal_places:Int32 )->this   [abstract]
    method print( value:Real32 )->this                         [abstract]
    method print( value:String )->this                         [abstract]
    method println->this                                       [abstract]
    method println( value:Byte )->this                         [abstract]
    method println( value:Character )->this                    [abstract]
    method println( value:Int32 )->this                        [abstract]
    method println( value:Logical )->this                      [abstract]
    method println( value:Int64 )->this                        [abstract]
    method println( value:Real64 )->this                       [abstract]
    method println( value:Real64, decimal_places:Int32 )->this [abstract]
    method println( value:Real32 )->this                       [abstract]
    method println( value:Object )->this                       [abstract]
    method println( value:String )->this                       [abstract]
    method write( buffer:StringBuilder )->this                 [abstract]
endClass

class PrintWriter<<$buffer>> : PrintWriter [aspect]
  METHODS
    method close->this
      noAction
      return this

    method flush->this
      write( $buffer )
      $buffer.clear
      return this

    method print( value:Byte )->this
      $buffer.print( value )
      return this

    method print( value:Character )->this
      $buffer.print( value )
      if (value == '\n') return flush
      return this

    method print( value:Int32 )->this
      $buffer.print( value )
      return this

    method print( value:Logical )->this
      $buffer.print( value )
      return this

    method print( value:Int64 )->this
      $buffer.print( value )
      return this

    method print( value:Real64, decimal_places:Int32 )->this
      $buffer.print( value, decimal_places )
      return this

    method print( value:Object )->this
      $buffer.print( value )
      return this

    method print( value:Real64 )->this
      $buffer.print( value )
      return this

    method print( value:Real32 )->this
      $buffer.print( value->Real64 )
      return this

    method print( value:String )->this
      $buffer.print( value )
      if ($buffer.count > 1024) flush
      return this

    method println->this
      $buffer.print( '\n' )
      return flush

    method println( value:Byte )->this
      return print( value ).println

    method println( value:Character )->this
      return print( value ).println

    method println( value:Int32 )->this
      return print( value ).println

    method println( value:Logical )->this
      return print( value ).println

    method println( value:Int64 )->this
      return print( value ).println

    method println( value:Real64 )->this
      return print( value ).println

    method println( value:Real64, decimal_places:Int32 )->this
      return print( value, decimal_places ).println

    method println( value:Real32 )->this
      return print( value ).println

    method println( value:Object )->this
      return print( value ).println

    method println( value:String )->this
      return print( value ).println

    method write( buffer:StringBuilder )->this [abstract]
endClass

class PrintWriterAdapter : PrintWriter<<buffer>>
  PROPERTIES
    output : Writer<<Byte>>
    buffer = StringBuilder()

  METHODS
    method init( output )

    method close->this
      output.close
      return this

    method flush->this
      write( buffer )
      buffer.clear
      output.flush
      return this

    method write( _buffer:StringBuilder )->this
      output.write( _buffer.utf8 )
      return this
endClass

